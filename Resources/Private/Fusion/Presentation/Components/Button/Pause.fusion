prototype(Jonnitto.PrettyEmbed:Presentation.Button.Pause) < prototype(Neos.Fusion:Component) {
    @propTypes {
        class = ${PropTypes.anyOf( PropTypes.string, PropTypes.arrayOf( PropTypes.string ) )}
        label = ${PropTypes.string}
        content = ${PropTypes.string.isRequired}
    }

    label = ${Translation.translate('pause', 'Pause', [], 'Main', 'Jonnitto.PrettyEmbedHelper')}
    class = ${BEM.modifier('jonnitto-prettyembed-button', 'pause')}
    file = ${Configuration.setting('Jonnitto.PrettyEmbed.button.pause')}
    content = ${File.exists(this.file) ? File.readFile(this.file) : null}

    @if.set = ${this.content}

    renderer = afx`
        <button
            aria-label={props.label}
            x-on:click="pause"
            x-show="playing"
            x-transition
            x-cloak
            type="button"
            class={props.class}
        >
            {props.content}
        </button>
    `
}
